apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

dependencies {
    compileOnly "androidx.annotation:annotation:${ANDROIDX_VERSION}"
    implementation("com.github.bumptech.glide:glide:${GLIDE_VERSION}"){
        exclude group:"com.android.support"
    }
    annotationProcessor "com.github.bumptech.glide:compiler:${GLIDE_VERSION}"
    compileOnly "androidx.fragment:fragment:${ANDROIDX_VERSION}"
    compileOnly "androidx.vectordrawable:vectordrawable-animated:${ANDROIDX_VERSION}"
}

android {
    namespace 'com.bumptech.glide.integration.webp'
    ndkVersion "${NDK_VERSION}"
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION as String

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        consumerProguardFiles "proguard-rules.pro"

        ndk {
            //abiFilter "armeabi"
            abiFilter "armeabi-v7a"
            abiFilter "arm64-v8a"
            abiFilter "x86"
            abiFilter "x86_64"
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }

    sourceSets {
        main {
            jni.srcDirs = []
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = GROUP
            artifactId = 'webpdecoder'

            afterEvaluate {
                from components.release
            }
        }
    }
}
